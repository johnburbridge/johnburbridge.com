/**
 * Default settings for all webapps
 */
description = "This project contains the defaults for all webapps";

subprojects {
    
    apply plugin: 'war';
    apply plugin: 'eclipse-wtp';
    apply plugin: 'project-report';
    
    /**
    * Import the cargo and cobertura plug-in definitions
    */
   buildscript {
       apply from: rootDir.path + '/cargo.gradle';
       apply from: rootDir.path + '/cobertura.gradle';
   }
   
    sourceCompatibility = 1.6;
    targetCompatibility = 1.6;
    
    dependencies {
        testCompile 'junit:junit:4.+';
        def cargoVersion = '1.1.1';
        cargo "org.codehaus.cargo:cargo-core-uberjar:$cargoVersion",
              "org.codehaus.cargo:cargo-ant:$cargoVersion",
              'jaxen:jaxen:1.1.1';
    }

    test {
        forkEvery = 10;
        maxParallelForks = 4;
    }

    /**
     * see the ~/.gradle/gradle.properties for more details on the credentials
     **/
    uploadArchives {
        repositories {
            ivy {
                url "${repositoryGradleLocalUrl}";
                credentials {
                    username "${artifactory_user}";
                    password "${artifactory_password}";
                }
            }
        }
    }
    uploadArchives.dependsOn(build);
    
    /**
     * initialize the project by creating it's the source, resources and webapp folders
     */
    task initProject << {
        sourceSets.all*.java.srcDirs*.each { it.mkdirs() }
        sourceSets.all*.resources.srcDirs*.each { it.mkdirs() }
        file(webAppDirName).mkdirs();
    }
}
