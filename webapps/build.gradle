/**
 * Default settings for all webapps
 * @author jburbridge
 */
description = "This project contains the defaults for all webapps";

subprojects {
    
    apply plugin: 'war';
    apply plugin: 'eclipse-wtp';
    apply plugin: 'project-report';
    
    sourceCompatibility = 1.6;
    targetCompatibility = 1.6;
    
    /**
     * Adds cargo support to all subprojects
     */
    buildscript {
        apply from: rootDir.path + '/cargo.gradle';
    }
    
    dependencies {
        testCompile 'junit:junit:4.+';
        def cargoVersion = '1.1.1';
        cargo "org.codehaus.cargo:cargo-core-uberjar:$cargoVersion",
              "org.codehaus.cargo:cargo-ant:$cargoVersion",
              'jaxen:jaxen:1.1.1';
    }

    test {
        forkEvery = 10;
        maxParallelForks = 4;
    }

    /**
     * Adds cobertura support to all subprojects
     */
    buildscript {
        apply from: rootDir.path + '/cobertura.gradle';
    }

    /**
    * see the root build.gradle for more details
    **/
    uploadArchives {
        repositories {
            ivy {
                url repositoryReleasesUrl;
                credentials {
                    username repositoryUsername;
                    password repositoryPassword;
                }
            }
        }
    }
   
    task initProject << {
        sourceSets.all*.java.srcDirs*.each { it.mkdirs() }
        sourceSets.all*.resources.srcDirs*.each { it.mkdirs() }
        file(webAppDirName).mkdirs();
    }
}